
# zebra_puzzles
:toc:
:toclevels: 6

## zebra_puzzles

## puzzle one

### question

* There are three houses in three different _positions_ .
* Each house has an occupant that lives alone .
* Each house has an different _color_ .
* The _color_ is either `blue` or `red` or `white` .
* Each occupant has an unique _nationality_ .
* The _nationality_ is either `italian` or `norwegian` or `palestinian` .


|====================================================
|             |  house #1  |  house #2  |  house #3
| position    |     1      |     2      |     3       
| color       |     ?      |     ?      |     ?       
| nationality |     ?      |     ?      |     ?       
|====================================================

1. The person with `palestinian` _nationality_ lives in an _position_ one greater than the _position_ of the house with _color_ `red` .
2. The person with `norwegian` _nationality_ lives in the house with _color_ `blue` .
3. The person with `italian` _nationality_ lives in the house at _position_ `2` .

### answer

###### ./src/parts/one.prolog
```

:- module(one,[]) .

:- [library(lists)] .

:- op(1,'xfy','contains') .

puzzle(HOUSEs)
:-
induktion(HOUSEs) ,
deduktion(HOUSEs)
.

induktion(HOUSEs)
:-
there_are_three_houses(HOUSEs) ,
the_set_of_positions_is_known(HOUSEs) ,
the_set_of_colors_is_known(HOUSEs) ,
the_set_of_nationalities_is_known(HOUSEs)
.

deduktion(HOUSEs)
:-
the_palestinian_lives_right_of_the_red_house(HOUSEs) ,
the_norwegian_lives_in_an_blue_house(HOUSEs) ,
the_italian_lives_in_position_two(HOUSEs)
.

there_are_three_houses(HOUSEs)
:-
length(HOUSEs,3)
.

the_set_of_positions_is_known(HOUSEs)
:-
lists:nth1(1,HOUSEs,{position:1,color:_,nationality:_}) ,
lists:nth1(2,HOUSEs,{position:2,color:_,nationality:_}) ,
lists:nth1(3,HOUSEs,{position:3,color:_,nationality:_})
.

the_set_of_colors_is_known(HOUSEs)
:-
HOUSEs contains {position:_,color:'blue',nationality:_} ,
HOUSEs contains {position:_,color:'red',nationality:_} ,
HOUSEs contains {position:_,color:'white',nationality:_}
.

the_set_of_nationalities_is_known(HOUSEs)
:-
HOUSEs contains {position:_,color:_,nationality:'italian'} ,
HOUSEs contains {position:_,color:_,nationality:'norwegian'} ,
HOUSEs contains {position:_,color:_,nationality:'palestinian'}
.

the_palestinian_lives_right_of_the_red_house(HOUSEs)
:-
HOUSEs contains {position:POSITION_PALESTINIAN,color:_,nationality:'palestinian'} ,
HOUSEs contains {position:POSITION_RED,color:'red',nationality:_} ,
POSITION_PALESTINIAN is POSITION_RED + 1
.

the_norwegian_lives_in_an_blue_house(HOUSEs)
:-
HOUSEs contains {position:_,color:'blue',nationality:'norwegian'}
.

the_italian_lives_in_position_two(HOUSEs)
:-
HOUSEs contains {position:2,color:_,nationality:'italian'}
.

HOUSEs contains HOUSE
:-
lists:member(HOUSE,HOUSEs)
.

```

There is 1 definitive solution to the puzzle .

###### ./src/parts/one.prolog.demo
```

/*
?- one:puzzle(HOUSEs) .
HOUSEs = [{position:1,color:blue,nationality:norwegian},{position:2,color:red,nationality:italian},{position:3,color:white,nationality:palestinian}] ;
false .
*/

```

#### tests

###### ./tests/parts/one.prolog
```
```

## puzzle two

### question

* There are three houses in three different _positions_ .
* Each house has an occupant that lives alone .
* Each house has a different _color_ .
* Each house has an _color_ that is either `blue` or `green` or `red` .
* Each occupant has unique _nationality_ of either `brazilian` or `german` or `chinese` .
* Each occupant has an different _pet_ .
* The _pet_ is either an `dog` or an `fish` or an `cat` .
* Every occupant plays an different _sport_ .
* The _sport_ is either `basketball` or `soccer` or `lacrosse` .

|====================================================
|             |  house #1  |  house #2  |  house #3
| position    |    1       |    2       |    3       
| color       |    ?       |    ?       |    ?       
| nationality |    ?       |    ?       |    ?       
| pet         |    ?       |    ?       |    ?       
| sport       |    ?       |    ?       |    ?       
|====================================================

1. The occupant with `brazilian` _nationality_ does not live at _position_ `2` .
2. The occupant with the `dog` as _pet_ plays the _sport_ of `basketball` .
3. In _position_ `2` the occupant plays the _sport_ of `lacrosse` .
4. The occupant with the _pet_ `fish` lives at one position less than the occupant with the _pet_ `cat` ,
5. The occupant with the _pet_ `dog` lives at one position greater than the `green` house .
6. The house with `red` _color_ is not beside the house with `blue` color .
7. The _pet_ `fish` is not in the house with the _color_ `red_ .

### answer

###### ./src/parts/two.prolog
```

:- module(two,[]) .


:- [library(lists)] .


:- op(1,'xfy','contains') .


puzzle(HOUSEs)
:-
induktion(HOUSEs) ,
deduktion(HOUSEs)
.


induktion(HOUSEs)
:-
there_are_three_houses(HOUSEs) ,
the_set_of_positions_is_known(HOUSEs) ,
the_set_of_colors_is_known(HOUSEs) ,
the_set_of_nationalities_is_known(HOUSEs) ,
the_set_of_pets_is_known(HOUSEs) ,
the_set_of_sports_is_known(HOUSEs)
.


deduktion(HOUSEs)
:-
the_brazilian_is_not_at_position_two(HOUSEs) ,
the_occupant_with_the_pet_dog_plays_sport_basketball(HOUSEs) ,
the_occupant_at_position_two_plays_lacrosse(HOUSEs) ,
the_occupant_with_pet_fish_lives_left_of_occupant_with_pet_cat(HOUSEs) ,
the_occupant_with_pet_dog_lives_right_of_green_house(HOUSEs) ,
the_red_house_is_not_beside_the_blue_house(HOUSEs) ,
the_pet_fish_is_not_in_the_red_house(HOUSEs)
.


there_are_three_houses(HOUSEs)
:-
length(HOUSEs,3)
.


the_set_of_positions_is_known(HOUSEs)
:-
lists:nth1(1,HOUSEs,{position:1,color:_,nationality:_,pet:_,sport:_}) ,
lists:nth1(2,HOUSEs,{position:2,color:_,nationality:_,pet:_,sport:_}) ,
lists:nth1(3,HOUSEs,{position:3,color:_,nationality:_,pet:_,sport:_})
.


the_set_of_colors_is_known(HOUSEs)
:-
HOUSEs contains {position:_,color:'blue',nationality:_,pet:_,sport:_} ,
HOUSEs contains {position:_,color:'green',nationality:_,pet:_,sport:_} ,
HOUSEs contains {position:_,color:'red',nationality:_,pet:_,sport:_}
.


the_set_of_nationalities_is_known(HOUSEs)
:-
HOUSEs contains {position:_,color:_,nationality:'brazilian',pet:_,sport:_} ,
HOUSEs contains {position:_,color:_,nationality:'german',pet:_,sport:_} ,
HOUSEs contains {position:_,color:_,nationality:'chinese',pet:_,sport:_}
.


the_set_of_pets_is_known(HOUSEs)
:-
HOUSEs contains {position:_,color:_,nationality:_,pet:'dog',sport:_} ,
HOUSEs contains {position:_,color:_,nationality:_,pet:'fish',sport:_} ,
HOUSEs contains {position:_,color:_,nationality:_,pet:'cat',sport:_}
.


the_set_of_sports_is_known(HOUSEs)
:-
HOUSEs contains {position:_,color:_,nationality:_,pet:_,sport:'basketball'} ,
HOUSEs contains {position:_,color:_,nationality:_,pet:_,sport:'soccer'} ,
HOUSEs contains {position:_,color:_,nationality:_,pet:_,sport:'lacrosse'}
.


the_brazilian_is_not_at_position_two(HOUSEs)
:-
+ HOUSEs contains {position:2,color:_,nationality:'brazilian',pet:_,sport:_}
.


the_occupant_with_the_pet_dog_plays_sport_basketball(HOUSEs)
:-
HOUSEs contains {position:_,color:_,nationality:_,pet:'dog',sport:'basketball'}
.


the_occupant_at_position_two_plays_lacrosse(HOUSEs)
:-
HOUSEs contains {position:2,color:_,nationality:_,pet:_,sport:'lacrosse'}
.


the_occupant_with_pet_fish_lives_left_of_occupant_with_pet_cat(HOUSEs)
:-
HOUSEs contains {position:POSITION_FISH,color:_,nationality:_,pet:'fish',sport:_} ,
HOUSEs contains {position:POSITION_CAT,color:_,nationality:_,pet:'cat',sport:_} ,
POSITION_FISH is POSITION_CAT - 1
.


the_occupant_with_pet_dog_lives_right_of_green_house(HOUSEs)
:-
HOUSEs contains {position:POSITION_DOG,color:_,nationality:_,pet:'dog',sport:_} ,
HOUSEs contains {position:POSITION_GREEN,color:'green',nationality:_,pet:_,sport:_} ,
POSITION_DOG is POSITION_GREEN + 1
.


the_occupant_in_position_three_has_nationality_german(HOUSEs)
:-
HOUSEs contains {position:3,color:_,nationality:'german',pet:_,sport:_}
.

the_red_house_is_not_beside_the_blue_house(HOUSEs)
:-
HOUSEs contains {position:POSITION_RED,color:'red',nationality:_,pet:_,sport:_} ,
HOUSEs contains {position:POSITION_BLUE,color:'blue',nationality:_,pet:_,sport:_} ,
DISTANCE is abs(POSITION_BLUE - POSITION_RED) ,
DISTANCE > 1
.

the_pet_fish_is_not_in_the_red_house(HOUSEs)
:-
+ HOUSEs contains {position:_,color:'red',nationality:_,pet:'fish',sport:_}
.


HOUSEs contains HOUSE
:-
lists:member(HOUSE,HOUSEs)
.

```

There 4 valid solutions to this puzzle .

###### ./src/parts/two.prolog.demo
```

/*
?- two:puzzle(HOUSEs) .
HOUSEs = [{position:1, color:blue, nationality:brazilian, pet:fish, sport:soccer}, {position:2, color:green, nationality:german, pet:cat, sport:lacrosse}, {position:3, color:red, nationality:chinese, pet:dog, sport:basketball}] ;
HOUSEs = [{position:1, color:blue, nationality:brazilian, pet:fish, sport:soccer}, {position:2, color:green, nationality:chinese, pet:cat, sport:lacrosse}, {position:3, color:red, nationality:german, pet:dog, sport:basketball}] ;
HOUSEs = [{position:1, color:blue, nationality:german, pet:fish, sport:soccer}, {position:2, color:green, nationality:chinese, pet:cat, sport:lacrosse}, {position:3, color:red, nationality:brazilian, pet:dog, sport:basketball}] ;
HOUSEs = [{position:1, color:blue, nationality:chinese, pet:fish, sport:soccer}, {position:2, color:green, nationality:german, pet:cat, sport:lacrosse}, {position:3, color:red, nationality:brazilian, pet:dog, sport:basketball}] ;
false .
*/

```

#### tests

###### ./tests/parts/two.prolog
```
```

### homework

Add one or more rules to this example so that there is one defininitive solution .

## puzzle three

* The police are trying to capture the gang of three culprits who have been stealing pumpkins .
* The first culprit is known by _name_ `Angela` .
* The second culprit is known by _name_ `Mary` .
* The third culprit is known by _name_ `David` .
* One of the culprits has _age_ of `5` ; one has _age_ of `7` ; one has _age_ of `8` .
* One has the _last name_ `Diamond` .
* The one with the _last name_ `Grant` is of an _age_ three years older than the _age_ of the one with the _last name_ `Leung` .

|==========================================================
|              |  culprit #1  |  culprit #2  | culprit #3
| age          |       ?      |       ?      |     ?
| first_name   |       ?      |       ?      |     ?
| last_name    |       ?      |       ?      |     ?
|==========================================================

### answer

###### ./src/parts/three.prolog
```

:- module(three,[]) .

:- [library(lists)] .

:- op(1,'xfy','contains') .

puzzle(CULPRITs)
:-
induktion(CULPRITs) ,
deduktion(CULPRITs)
.

induktion(CULPRITs) :-
there_are_three_culprits(CULPRITs) ,
the_set_of_last_names_is_known(CULPRITs) ,
the_set_of_ages_is_known(CULPRITs)
.

deduktion(CULPRITs)
:-
one_is_three_years_older(CULPRITs)
.

there_are_three_culprits(CULPRITs)
:-
length(CULPRITs,3) ,
lists:nth1(1,CULPRITs,{position:1,first_name:'Angela',last_name:_,age:_}) ,
lists:nth1(2,CULPRITs,{position:2,first_name:'Mary',last_name:_,age:_}) ,
lists:nth1(3,CULPRITs,{position:3,first_name:'David',last_name:_,age:_})
.

the_set_of_last_names_is_known(CULPRITs)
:-
CULPRITs contains {position:_,first_name:_,last_name:'Diamond',age:_} ,
CULPRITs contains {position:_,first_name:_,last_name:'Grant',age:_} ,
CULPRITs contains {position:_,first_name:_,last_name:'Leung',age:_}
.

the_set_of_ages_is_known(CULPRITs)
:-
CULPRITs contains {position:_,first_name:_,last_name:_,age:5} ,
CULPRITs contains {position:_,first_name:_,last_name:_,age:7} ,
CULPRITs contains {position:_,first_name:_,last_name:_,age:8}
.

one_is_three_years_older(CULPRITs)
:-
CULPRITs contains {position:_,first_name:_,last_name:'Grant',age:AGE_GRANT} ,
CULPRITs contains {position:_,first_name:_,last_name:'Leung',age:AGE_LEUNG} ,
AGE_GRANT is AGE_LEUNG + 3
.

CULPRITs contains CULPRIT
:-
lists:member(CULPRIT,CULPRITs)
.

```

There is not an definitive answer to this puzzle .
With the given clues there are still 6 possible solutions .

###### ./src/parts/three.prolog.demo
```

/*
?- three:puzzle(CULPRITs) .
CULPRITs = [{position:1,first_name:'Angela',last_name:'Diamond',age:7},{position:2,first_name:'Mary',last_name:'Grant',age:8},{position:3,first_name:'David',last_name:'Leung',age:5}] ;
CULPRITs = [{position:1,first_name:'Angela',last_name:'Diamond',age:7},{position:2,first_name:'Mary',last_name:'Leung',age:5},{position:3,first_name:'David',last_name:'Grant',age:8}] ;
CULPRITs = [{position:1,first_name:'Angela',last_name:'Grant',age:8},{position:2,first_name:'Mary',last_name:'Diamond',age:7},{position:3,first_name:'David',last_name:'Leung',age:5}] ;
CULPRITs = [{position:1,first_name:'Angela',last_name:'Leung',age:5},{position:2,first_name:'Mary',last_name:'Diamond',age:7},{position:3,first_name:'David',last_name:'Grant',age:8}] ;
CULPRITs = [{position:1,first_name:'Angela',last_name:'Grant',age:8},{position:2,first_name:'Mary',last_name:'Leung',age:5},{position:3,first_name:'David',last_name:'Diamond',age:7}] ;
CULPRITs = [{position:1,first_name:'Angela',last_name:'Leung',age:5},{position:2,first_name:'Mary',last_name:'Grant',age:8},{position:3,first_name:'David',last_name:'Diamond',age:7}] ;
false .
*/

```

#### tests

###### ./tests/parts/three.prolog
```
```

## puzzle four

### question

* Four boys have decided to watch an movie .
* Each boy has an _position_ to sit in .
* Each boy has an _name_ .
* Each boy has an _age_ .
* Each boy likes a kind of _movie_ .
* Each boy will eat an _snack_ .
* Each boy has an color of _shirt_ .

|=====================================================
|          |  boy #1  |  boy #2  |  boy #3  |  boy #4
| position |    1     |    2     |    3     |    4
| name     |    ?     |    ?     |    ?     |    ?
| age      |    ?     |    ?     |    ?     |    ?
| movie    |    ?     |    ?     |    ?     |    ?
| snack    |    ?     |    ?     |    ?     |    ?
| shirt    |    ?     |    ?     |    ?     |    ?
|=====================================================

1. The boy with _name_ `Joshua` is in left-most _position_ `1` or right-most _position_ `4`
2. The boy with the `black` _shirt_ is somewhere to the left of the boy with the smallest _age_ .
3. The boy with _name_ `Joshua` likes `horror` movies .
4. The boy with _age_ `14` is at _position_ `3` .
5. The boy wearing the `red` _shirt_ is in an position right of the boy with _age_ `13` and in an position left of the boy likes `action` movies .
6. The boy with _name_ `Daniel` likes `thriller` _movies_.
7. The boy who is going to _snack_ with `cookies` is in one of the end positions .
8. The boy wearing the `black` _shirt_ is exactly to the left of the boy who likes `thriller` movies.
9. The boy who is going to _snack_ with `crackers` is exactly to the right of the boy who likes `comedy` _movies_ .
10. The boy wearing the `red` _shirt_ is somewhere to the right of the boy who is going to _snack_ with `popcorn` and somewhere to the left of the boy with _name_ `Nicholas` .
11. In one of the end positions is the boy who likes `Thriller` _movies_ .
12. The boy with _name_ `Nicholas` is somewhere to the right of the boy with _name_ `Joshua` and to the left of the boy with _name_ `Daniel` .
13. In the first position is the boy wearing the `green` _shirt_ .

### answer

#### homework

Provide an solution , the demo , and it's tests for this puzzle
in the style of the earlier answers .

Does the puzzle have a definitive answer ?
Is the puzzle impossible to solve ?

###### ./src/parts/four.prolog
```
```

###### ./src/parts/four.prolog.demo
```
```

#### tests

###### ./tests/parts/four.prolog
```
```

## puzzle five

_The classic zebra puzzle_

### question

There are 5 houses, occupied by politically-incorrect
gentlemen of 5 different _nationalities_, who all have different
_coloured_ houses, keep different _pets_, _drink_ different drinks, and _smoke_
different  (now-extinct) brands of cigarettes.

* The _nationality_ of the occupant is one of `english` , `spanish` , `ukranian` , `norwegian` , or `japanese` .
* The _color_ of each house is one of `red` , `green` , `ivory` , `yellow` , or `blue` .
* The _pet_ is one of `dog` , `snail` , `fox` , `horse` , or `zebra` .
* The drink is one of `coffee` , `tea` , `milk` , `orange juice` , or `water` .
* The smoke is one of `Old Gold` , `Kools` , `Chesterfields` , `Lucky Strikes` , or `Parliaments` .

|==============================================================================
|             |  house #1  |  house #2  |  house #3  |  house #4  |  house #5  
| position    |    1       |    2       |    3       |    4       |    5       
| color       |    ?       |    ?       |    ?       |    ?       |    ?       
| nationality |    ?       |    ?       |    ?       |    ?       |    ?       
| pet         |    ?       |    ?       |    ?       |    ?       |    ?       
| drinks      |    ?       |    ?       |    ?       |    ?       |    ?       
| smokes      |    ?       |    ?       |    ?       |    ?       |    ?       
|==============================================================================

1. The occupant with `english` _nationality_ lives in the `red` house .
2. The occupant with `spanish` _nationality_ has an _pet_ `dog`.
3. The occupant with the `green` _color_ of house _drinks_ `coffee` .
4. The occupant with `ukrainian` _nationality_ _drinks_ `tea` .
5. The `ivory` _colored_ house is in an position one less than the _position_ of the house with _color_ `green` .
6. The owner of the _pet_ `snail` _smokes_ `Old Gold` .
7. The owner of the house with _color_ `yellow` _smokes_ `Kools` .
8. The owner of the house in _position_ `2` _drinks_ `milk` .
9. The _nationality_ of the occupant in _position_ `1` is `norwegian` .
10. The `Chesterfields` _smoker_ lives next to a house with an _pet_ `fox` .
11. The _pet_ `horse` owner lives in an _position_ next to the `Kools` _smoker_ .
12. The `Lucky Strikes` _smoker_ _drinks_ `orange juice` .
13. The occupant with _nationality_ `japanese` _smokes_ `Parliaments` .
14. The occupant with _nationality_ `norwegian` lives in an _position_ next to the house with _color_ `blue` .

#### history

```
% cat <zebra.pl
%  Prolog program to solve the zebra problem.
%  Aug 1984, Dan Sahlin, The Royal Inst. of Techn., Stockholm, Sweden
% (revised October, 1985, P. H. Roosen-Runge)


        :- op(100,xfy,on).  % a bit of grammar

% Who owns the zebra and who drinks water?
find(ZebraOwner,WaterDrinker) :-

%       ************  THE FACTS **************************************
%    There are 5 houses, occupied by politically-incorrect
%    gentlemen of 5 different nationalities, who all have different
%    coloured houses, keep different pets, drink different drinks, and smoke
%    different  (now-extinct) brands of cigarettes.

        makehouses(5, List),                                        %1
%    The Englishman lives in a red house.
        house(  red,  englishman,    _,      _,       _)   on List, %2
%    The Spaniard keeps a dog.
        house(    _,    spaniard,  dog,      _,       _)   on List, %3
%    The owner of the green house drinks coffee.
        house(green,           _,    _, coffee,       _)   on List, %4
%    The Ukrainian drinks tea.
        house(    _,   ukrainian,    _,    tea,       _)   on List, %5
%    The ivory house is just to the left of the green house.
      sublist2( [house(ivory,           _,    _,      _,       _) ,
                house(green,           _,    _,      _,       _)], List), %6
%    The owner of a pet snail smokes Old Gold.
        house(    _,           _,snail,      _,old_gold)   on List, %7
%    The owner of the yellow house smokes Kools.
        house(yellow,          _,    _,      _,   kools)   on List, %8
%    The owner of the third house drinks milk.
     List = [_, _,
        house(    _,           _,    _,   milk,       _),
             _, _],                                                 %9
%    A Norwegian owns the first house.
     List =
       [house(    _,   norwegian,    _,      _,       _) | _], %10
%    The Chesterfields smoker lives next to a house with a fox.
 nextto(house(    _,           _,    _,      _, chesterfields),
        house(    _,           _,  fox,      _,         _), List),  %11
%   The horse owner lives next to the Kools smoker.
 nextto(house(    _,           _,    _,      _,    kools),
        house(    _,           _,horse,      _,        _), List),   %12
%   The Lucky Striker smoker drinks orange juice.
        house(    _,           _,    _, orange, lucky_strike) on List, %13
%   The Japanese smokes Parliaments.
        house(    _,    japanese,    _,      _, parliaments ) on List, %14
%   The Norwegian lives next to a blue house.
 nextto(house(    _,   norwegian,    _,      _,          _),
        house( blue,           _,    _,      _,          _), List), %15

        house(    _, WaterDrinker,    _,  water,          _)  on List,
        house(    _,  ZebraOwner,zebra,      _,          _)  on List.


%       ********** DEFINITIONS ************************************

        makehouses(0,[]).

        makehouses(N,[house(_, _, _, _, _)|List])
                :- N>0, N1 is N - 1, makehouses(N1,List).

        X on List :- member(X, List).

        sublist2([S1, S2], [S1, S2 | _]) .
        sublist2(S, [_ | T]) :- sublist2(S, T).

        nextto(H1, H2, L) :- sublist2([H1, H2], L).
        nextto(H1, H2 ,L) :- sublist2([H2, H1], L).  
```

### answer

#### homework

Provide an solution , the demo , and it's tests for this puzzle
in the style of the earlier answers .

This puzzle has an definitive answer.

###### ./src/parts/five.prolog
```

:- module(five,[]) .

```

###### ./src/parts/five.prolog.demo
```
```

#### tests

###### ./tests/parts/five.prolog
```
```

## appendix

### authorship

* (c) kintalken@gmail.com 2020-10-23 .
